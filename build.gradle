// Note 'apply false' for each plugin. This ensures they aren't applied to the root project (here),
// but it makes the plugins available for subprojects.
plugins {
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06" apply false
    id "com.snobot.simulator.plugin.SnobotSimulatorPlugin" version "2018-1.0.0" apply false
}


allprojects
{
    apply plugin: "eclipse"
    apply plugin: "checkstyle"
    apply plugin: "pmd"
    apply plugin: "findbugs"

    repositories {
        mavenCentral()
    }
    
    checkstyle {
        toolVersion = "8.1"
        configFile = new File(rootDir, "styleguide/checkstyle.xml")
    }
    
    pmd {
        consoleOutput  = true
        reportsDir = file("${project.buildDir}/reports/pmd")
        ruleSetFiles = files(file("$rootDir/styleguide/pmd-ruleset.xml"))
        ruleSets = []
    }
    
    findbugs {
        excludeFilter = file("$rootDir/styleguide/findbugs_suppressions.xml")
        effort = "max"
    }
    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}
