buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jacoco"
    id "checkstyle"
    id "pmd"
    id "findbugs"
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.21"
    id "com.snobot.simulator.plugin.SnobotSimulatorPlugin" version "2018.01.10"
}

apply plugin: com.snobot.simulator.plugin.SnobotSimulatorPlugin
snobotSimVersions {
    snobotSimVersion = "0.7.3"
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
    }
    
    test
    {
        java
        {
            srcDirs = ["test/unit_tests"]
        }
    }
}

dependencies {

    compile wpilib()
    compile ctre()
    
    // Third Party without maven
    compile files('dependencies/RIOdroid.jar')
    
    // Simulator
    testCompile snobotSimCompile()
    testCompile snobotSimJavaCompile()
    
    // Testing
    testCompile 'junit:junit:4.12'
}

test {
	ignoreFailures = true
}

//////////////////////////////
// Style/Formatting stuff
checkstyle {
    toolVersion = "8.1"
    configFile = new File(rootDir, "styleguide/checkstyle.xml")
}

pmd {
    consoleOutput  = true
    reportsDir = file("${project.buildDir}/reports/pmd")
    ruleSetFiles = files(file("$rootDir/styleguide/pmd-ruleset.xml"))
    ruleSets = []
}

findbugs {
    excludeFilter = file("$rootDir/styleguide/findbugs_suppressions.xml")
    effort = "max"
    ignoreFailures = true
}
//////////////////////////////

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,  exclude: 
                [
                'com/team254/lib/trajectory/**',
                'com/snobot/lib/motion_profile/**',
                'com/snobot/lib/vision/**',
                'com/snobot/lib/external_connection/**',
                'com/snobot/lib/adb/**',
                ])
        })
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
